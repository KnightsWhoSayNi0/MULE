; int strncasecmp ( const char *str1 , const char *str2 , int len );
	assume	adl=1

	public	_strncasecmp
_strncasecmp:
	push	ix
	ld	ix,0
	add	ix,sp
	dec	sp
loop:
	ld	hl,(ix+12)
	add	hl,de
	or	a,a
	sbc	hl,de
	jr	z,done
	dec	hl
	ld	(ix+12),hl
	ld	hl,(ix+6)
	ld	c,(hl)
	inc	hl
	ld	(ix+6),hl
	push	bc
	call	_tolower
	pop	bc
	ld	(ix-1),l
	ld	hl,(ix+9)
	ld	c,(hl)
	inc	hl
	ld	(ix+9),hl
	push	bc
	call	_tolower
	pop	bc
	ex	de,hl
	sub	a,a
	sbc	hl,hl
	ld	l,(ix-1)
	sbc	hl,de
	jr	nz,done
	ld	a,(ix-1)
	or	a,e
	jr	nz,loop
done:
	ld	sp,ix
	pop	ix
	ret

	extern	_tolower
